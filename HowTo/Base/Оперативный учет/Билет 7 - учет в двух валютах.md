
## Текст задачи

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Весь учет ведется одновременно в 2-х валютах: рубли и доллары. При проведении документов курс указывается непосредственно в самом документе. Возникновение курсовых разниц на себестоимость при продаже не предполагается.

Складской учет товаров не ведется.

Списание себестоимости товаров должно быть организовано по партиям, в зависимости метода списания (FIFO или LIFO), принятого в учетной политике. Значение учетной политики меняется не чаще одного раза в год. При проведении документа необходимо использовать метод, актуальный на момент проведения.

Необходимо построить отчет по продажам товаров за период.

Продажи с 01.01.2010 по 31.03.2010
![[Pasted image 20241104095853.png]]

Прибыль по каждой валюте рассчитывается как:

_«Сумма продаж» - «Себестоимость»_

# Структура регистров

![[Pasted image 20241104100052.png]]

## Документ приходная накладная

![[Pasted image 20241104121416.png]]


### Модуль документа Приходная накладная

```1C

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Партия = Ссылка;
		Движение.СуммаВРублях = ТекСтрокаСписокНоменклатуры.Сумма;
		Движение.СуммаВВалюте = ?(Курс = 0, 0, ТекСтрокаСписокНоменклатуры.Сумма / Курс);
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество
	КонецЦикла;

КонецПроцедуры

```

## Документ Расходная накладная

![[Pasted image 20241104121514.png]]

### Модуль объекта Приходная накладная

```1C

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	МетодСписания = РегистрыСведений.УчетнаяПолитикаПредприятия.ПолучитьПоследнее(Дата).МетодСписанияСебестоимости;
	
	Если НЕ ЗначениеЗаполнено(МетодСписания) Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен метод списания себестоимости";
		Сообщение.Сообщить();                                      
		Возврат;
	КонецЕсли;                                                     
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВТ_Товары.Количество КАК КоличествоВДокументе,
	|	ВТ_Товары.Сумма КАК СуммаВДокументе,
	|	ВЫБОР
	|		КОГДА ВТ_Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоТовар,
	|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаВРубляхОстаток, 0) КАК СуммаВРубляхОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаВВалютеОстаток, 0) КАК СуммаВВалютеОстаток,
	|	ВТ_Товары.Номенклатура.Представление КАК НоменклатураПредставление
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						ВТ_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиНоменклатурыОстатки
	|		ПО ВТ_Товары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(КоличествоВДокументе),
	|	МАКСИМУМ(СуммаВДокументе),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "УБЫВ", "");
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		// Вставить обработку выборки ВыборкаНоменклатура
		СтоимостьВРублях = 0; // для накопления списанной стоимости для помещения в регистр продаж
		СтоимостьВВалюте = 0; // для накопления списанной стоимости в валюте для помещения в регистр продаж
		
		Если ВыборкаНоменклатура.ЭтоТовар = ЛОЖЬ Тогда
			Продолжить;
		КонецЕсли;
		
		Превышение = ВыборкаНоменклатура.КоличествоВДокументе - ВыборкаНоменклатура.КоличествоОстаток;
		
		Если Превышение > 0 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Превышение остатка по номеклатуре %1 в количестве %2", 
			ВыборкаНоменклатура.НоменклатураПредставление, Превышение);
			Сообщение.Сообщить();
		КонецЕсли;	
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.КоличествоВДокументе;
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		
		
		Пока ВыборкаДетальныеЗаписи.Следующий()  И ОсталосьСписать <> 0 Цикл
			Списываем = Мин(ВыборкаДетальныеЗаписи.КоличествоОстаток, ОсталосьСписать);
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
			Движение.Количество = Списываем;				
			Движение.СуммаВРублях = Списываем / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СуммаВРубляхОстаток;
			Движение.СуммаВВалюте = Списываем / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СуммаВВалютеОстаток;
			
			ОсталосьСписать = ОсталосьСписать - Списываем;
			СтоимостьВРублях = СтоимостьВРублях + Движение.СуммаВРублях;
			СтоимостьВВалюте = СтоимостьВВалюте + Движение.СуммаВВалюте;
			
		КонецЦикла;
		
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество = ВыборкаНоменклатура.КоличествоВДокументе;
		Движение.СтоимостьВРублях = СтоимостьВРублях;
		Движение.СтоимостьВВалюте = СтоимостьВВалюте;
		Движение.ВыручкаВРублях = ВыборкаНоменклатура.СуммаВДокументе;
		Движение.ВыручкаВВалюте = ?(Курс = 0, 0, ВыборкаНоменклатура.СуммаВДокументе / Курс);
	КонецЦикла;
	
	
	
КонецПроцедуры

```

## Отчет СКД

Запрос отчета

```
ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.КоличествоОборот КАК Количество,
	ПродажиОбороты.СтоимостьВРубляхОборот КАК СтоимостьВРублях,
	ПродажиОбороты.СтоимостьВВалютеОборот КАК СтоимостьВВалюте,
	ПродажиОбороты.ВыручкаВРубляхОборот КАК ВыручкаВРублях,
	ПродажиОбороты.ВыручкаВВалютеОборот КАК ВыручкаВВалюте
ИЗ
	РегистрНакопления.Продажи.Обороты КАК ПродажиОбороты
```

![[Pasted image 20241104121841.png]]

![[Pasted image 20241104121848.png]]

![[Pasted image 20241104121855.png]]

![[Pasted image 20241104121903.png]]

![[Pasted image 20241104121911.png]]