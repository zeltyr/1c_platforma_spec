## 1.  Регистр накопления "Отгрузки"

Регистр накопления **Оборотный**. 

Структура регистра. 

**Измерения:** 
- Номенклатура
**Ресурсы:**
- Количество
- Сумма
- КоличествоПолучено
- СуммаПолучено

![[Pasted image 20241112200222.png]]


## Регистр Накопления Взаиморасчеты

Вид регистра  - Остатки.

**Измерения:**
- Контрагент
- Накладная

**Ресурсы:
- Сумма

![[Pasted image 20241112204632.png]]

## 3. Документ "Расходная накладная"

Проектируем документ расходная накладная.
В шапку добавляем контрагента, в ТЧ добавляем колонки КоличествоПолучено и СуммаПолучено.

### Модуль объекта

```1C

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПолучено = СписокНоменклатуры.Итог("СуммаПолучено");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// ДВИЖЕНИЯ ПО РЕГИСТРУ ОТГРУЗКИ
	
	Движения.Отгрузки.Записывать = Истина;
	
	// Запрос получения данных для формирования движений в регистре Отгрузки
	Запрос = Новый Запрос;
	Запрос.Текст =  
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.КоличествоОтгружено) КАК КоличествоОтгружено,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.СуммаОтгружено) КАК СуммаОтгружено,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.КоличествоПолучено) КАК КоличествоПолучено,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.СуммаПолучено) КАК СуммаПолучено
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	// Обход результата запроса, формирования движений в регистр Отгрузки
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.Отгрузки.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.КоличествоОтгружено = Выборка.КоличествоОтгружено;
		Движение.СуммаОтружено = Выборка.СуммаОтгружено;
		Движение.КоличествоПолучено = Выборка.КоличествоПолучено;
		Движение.СуммаПолучено = Выборка.СуммаПолучено;
	КонецЦикла;

	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ ВЗАИМОРАСЧЕТЫ
	
	Движения.Взаиморасчеты.Записывать = Истина;
	
	Движение = Движения.Взаиморасчеты.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;	
	Движение.Накладная = Ссылка;
	Движение.Сумма = СуммаПолучено;
	
КонецПроцедуры

```

## 4. Документ Приход Денег

Документ является регистратором для РН Взаиморасчеты и Отгрузки.

Структура документа.
Добавляем контрагента в шапку.
Добавляем ТЧ список накладных с одним реквизитом Накладная.

Док делает движения в РН Взаиморасчеты.

![[Pasted image 20241112205252.png]]


### 4.1 Модуль объекта

```1C

Процедура ОбработкаПроведения(Отказ, Режим)
	//Старая методика
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	
	//устанавливаем блокировку
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНакладных;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Накладная", "Накладная");
	Блокировка.Заблокировать();	
	
	// Получение данных для формирования движений (остатки по накладным из табличной части по регистру Взаиморасчеты)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходДенегСписокНакладных.Накладная КАК Накладная
		|ПОМЕСТИТЬ ВТ_ТабЧасть
		|ИЗ
		|	Документ.ПриходДенег.СписокНакладных КАК ПриходДенегСписокНакладных
		|ГДЕ
		|	ПриходДенегСписокНакладных.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Накладная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТабЧасть.Накладная.Ссылка КАК Накладная,
		|	ВзаморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	ВТ_ТабЧасть КАК ВТ_ТабЧасть
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Взаиморасчеты.Остатки(
		|				&МоментВремени,
		|				Контрагент = &Контрагент
		|					И Накладная В
		|						(ВЫБРАТЬ
		|							ВТ_ТабЧасть.Накладная КАК Накладная
		|						ИЗ
		|							ВТ_ТабЧасть КАК ВТ_ТабЧасть)) КАК ВзаморасчетыОстатки
		|		ПО ВТ_ТабЧасть.Накладная = ВзаморасчетыОстатки.Накладная
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ТабЧасть.Накладная.МоментВремени";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	// Распределение суммы документа по накладным, формирование расходных движений в регистр Взаиморасчеты
	ОсталосьСписать = СуммаОплаты;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() И ОсталосьСписать <> 0  Цикл
		
		Списываем = Мин(ОсталосьСписать, Выборка.СуммаОстаток);
		
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Выборка.Накладная;
		Движение.Сумма = Списываем;
		
		ОсталосьСписать = ОсталосьСписать - Списываем;
		
	КонецЦикла;
	
	Если ОсталосьСписать > 0 Тогда

		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Переплат быть не должно! Сумма оплаты превышает долг по всем накладным на %1", 
			ОсталосьСписать);
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

```


