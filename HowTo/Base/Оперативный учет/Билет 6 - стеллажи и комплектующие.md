
## Текст задачи
Компания занимается оптовой торговлей складских стеллажей и их комплектующих. Закупка комплектующих отражается документом «Приходная Накладная», продажа – «Расходная накладная».

 Каждый стеллаж представляет собой некоторый фиксированный набор комплектующих (например, 4 стойки, 5 полок и 20 болтов). Необходимо обеспечить уникальность деталей, т.е. одна и та же деталь не может относиться к разным стеллажам.

 Учет остатков ведется в разрезе складов. В документах «приходная накладная» и «Расходная накладная» склад только один (склад – реквизит шапки).

Возможна продажа как отдельных комплектующих, так и целых стеллажей, причем и стеллажи и их комплектующие указываются в одной табличной части. В случае продажи стеллажа осуществляется списание со склада соответствующего количества комплектующих. В том случае, если каких-либо комплектующих на складе не хватает, документ проводиться не должен. Учет себестоимости деталей вести не требуется.

Используя планы видов характеристик, необходимо предоставить возможность пользователю добавить произвольное количество характеристик к каждому стеллажу (материал, страна).

Создать отчет, который в разрезе складов будет показывать количество стеллажей. Например, если стеллаж состоит из 4 стоек, 5 полок и 20 болтов, а на складе есть 8 стоек, 15 полок и 30 болтов, то целый стеллаж только один.

Также в отчете вывести информацию о стране стеллажа.

Наличие деталей на дату 31 января 2021 г

##  Создание ПВХ и РС для хранения информации о стеллажах и комп-их

		1. Первым делом создаем справочник СвойстваОбъектов у которого владельцем указываем  ПВХ свойстйва объектов

	
![[Pasted image 20241030215316.png]]


	2. Далее создаем Непериодический независимый регистр сведений ЗначенияСвойств.
	   2.1. Измерение **Стеллаж** = Тип(Справочник.Номенклатура). с отбором по виду номенклатуры (стеллаж) в параметрах выбора.
	   2.2. Измерение **Характеристика** = Тип(ПланВидовХарактеристикСсылка.СвойстваОбъектов) 
	   2.3. Ресурс **Значение** = Тип(Характеристика.СвойстваОбъектов). Связи параметров выбора = Отбор.Владелец(Характеристика).
	   связь по типу = Характеристика.

![[Pasted image 20241030215510.png]]	

	3. В Справочнике Номенклатура  для характеристик настраиваем связи

![[Pasted image 20241030220132.png]]


	4. Создаем независимый непериодияеский регистр сведений Состав Стеллажей.
	   
	Структура


![[Pasted image 20241030224956.png]]	   

где *Деталь = СправочникСсылка.Номенклатура  и ПараметрыВыбора = Отбор.ВидНоменклатуры(Комплектующее

*Стеллаж = СправочникСсылка.Номенклатура и ПараметрыВыборка = Отбор.ВидНоменклатуры(Стеллаж)*

Количество = Число(12,2)

## Документ приходная накладная

![[Pasted image 20241101234153.png]]


### Модуль объекта Приходная накладная

``` 1C

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
	КонецЦикла;

КонецПроцедуры

```

## Документ расходная накладная

![[Pasted image 20241101234259.png]]



### Модуль объекта Расходная накладная

```1C


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// ДВИЖЕНИЯ ПО РЕГИСТРУ ОСТАТКИ НОМЕНКЛАТУРЫ (НОВАЯ МЕТОДИКА ПРОВЕДЕНИЯ)
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(СоставСтеллажей.Деталь, РасходнаяНакладнаяСписокНоменклатуры.Номенклатура) КАК Номенклатура,
	               |	СУММА(ЕСТЬNULL(СоставСтеллажей.Количество, 1) * РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	               |ПОМЕСТИТЬ ВТ_Товары
	               |ИЗ
	               |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
	               |		ПО РасходнаяНакладнаяСписокНоменклатуры.Номенклатура = СоставСтеллажей.Стеллаж
	               |ГДЕ
	               |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЕСТЬNULL(СоставСтеллажей.Деталь, РасходнаяНакладнаяСписокНоменклатуры.Номенклатура)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Товары.Номенклатура КАК Номенклатура,
	               |	ВТ_Товары.Количество КАК Количество
	               |ИЗ
	               |	ВТ_Товары КАК ВТ_Товары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	// Формирование набора записей, запись движений в регистр
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();		
		Движение.Период = Дата;
		Движение.Склад = Склад;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.Записать();
	
	// Запрос получения данных для контроля появления отрицательных остатков
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Превышение,
	|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						Т.Номенклатура
	|					ИЗ
	|						ВТ_Товары КАК Т)
	|				И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
	|ГДЕ
	|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
	
	// Если результат запроса не пустой, то вывод данных по превышениям остатка и отказ от проведения документа
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2",
				Выборка.НоменклатураПредставление, Выборка.Превышение);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

```


##  Регистр накопления Остатки номенклатуры

![[Pasted image 20241101234405.png]]

## Отчет наличие деталей

Для отчета настройки характеристик  нужно  загрузить в пользовательском режиме

### Запрос отчета 

``` 1C
ВЫБРАТЬ
	СоставСтеллажей.Стеллаж КАК Стеллаж,
	ОстаткиНоменклатурыОстатки.Склад КАК Склад
ПОМЕСТИТЬ ВТ_СтелажиПоСкладамСДеталямиВОстатках
ИЗ
	РегистрНакопления.ОстаткиНоменклатуры.Остатки КАК ОстаткиНоменклатурыОстатки
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
		ПО ОстаткиНоменклатурыОстатки.Номенклатура = СоставСтеллажей.Деталь

ИНДЕКСИРОВАТЬ ПО
	Стеллаж
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_СтелажиПоСкладамСДеталямиВОстатках.Стеллаж КАК Стеллаж,
	ВТ_СтелажиПоСкладамСДеталямиВОстатках.Склад КАК Склад,
	СоставСтеллажей.Деталь КАК Деталь,
	СоставСтеллажей.Количество КАК Количество
ПОМЕСТИТЬ ВТ_ДанныеПоСоставуСтеллажей
ИЗ
	ВТ_СтелажиПоСкладамСДеталямиВОстатках КАК ВТ_СтелажиПоСкладамСДеталямиВОстатках
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
		ПО ВТ_СтелажиПоСкладамСДеталямиВОстатках.Стеллаж = СоставСтеллажей.Стеллаж

ИНДЕКСИРОВАТЬ ПО
	Деталь,
	Стеллаж
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ДанныеПоСоставуСтеллажей.Склад КАК Склад,
	ВТ_ДанныеПоСоставуСтеллажей.Стеллаж КАК Стеллаж,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) / ВТ_ДанныеПоСоставуСтеллажей.Количество КАК Количество
ИЗ
	ВТ_ДанныеПоСоставуСтеллажей КАК ВТ_ДанныеПоСоставуСтеллажей
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки КАК ОстаткиНоменклатурыОстатки
		ПО ВТ_ДанныеПоСоставуСтеллажей.Склад = ОстаткиНоменклатурыОстатки.Склад
			И ВТ_ДанныеПоСоставуСтеллажей.Деталь = ОстаткиНоменклатурыОстатки.Номенклатура
```

![[Pasted image 20241101234501.png]]

![[Pasted image 20241101234511.png]]

![[Pasted image 20241101234521.png]]

![[Pasted image 20241101234530.png]]

![[Pasted image 20241101234543.png]]

