
## 1. Создаем регистр  накопления "Заказы покупателей"

Вид регистра - **Остатки**

**Ресурсы:**
	- ЗаказПокупателя
	- Номенклатура
**Измерения:**
	- Количество
**Регистраторы:**
	- Заказ Покупателя
	- Приходная накладная
	  
![[Pasted image 20241113225356.png]]


## 2. Регистр накопления Остатки.

Регистраторами будут  ==Расходная== и ==Приходная== накладная.

**Измерения:**
- Номенклатура
- Заказ покупателя
Ресурсы:
- Количество
- Сумма

![[Pasted image 20241113230726.png]]

## 3. Документ заказ покупателя


Реквизитов нет.
ТЧ - Список номенклатуры
	Реквизиты ТЧ 
		- Номенклатура
		- Количество
		  
Является регистратором для регистра Заказы Покупателей.
Является ==основание для ввода== Приходной накладной.
Я воспользовался конструктором.

### 3.1 Модуль объекта

```1C

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЗаказыПокупателей Приход
	Движения.ЗаказыПокупателей.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ЗаказыПокупателей.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.ЗаказПокупателя = Ссылка;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
	КонецЦикла;
КонецПроцедуры

```
## 4. Документ Приходная накладная

В ТЧ Список Номенклатуры добавляем колонку Заказ Покупателя.
Заполнение колонки обязательно. 
Документ делает движения по регистру Остатки Номенклатуры и Заказы Покупателей.

### 4.1 Модуль объекта Приходная накладная

```1C

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		// Заполнение шапки
		Для Каждого ТекСтрокаСписокНоменклатуры Из ДанныеЗаполнения.СписокНоменклатуры Цикл
			НоваяСтрока = СписокНоменклатуры.Добавить();
			НоваяСтрока.Количество = ТекСтрокаСписокНоменклатуры.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
			НоваяСтрока.ЗаказПокупателя = ДанныеЗаполнения;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ЗаказыПокупателей Расход
	//НОВАЯ МЕТОДИКА
	Движения.ЗаказыПокупателей.Записывать = Истина;
	Движения.ЗаказыПокупателей.БлокироватьДляИзменения = Истина;
	
	//Делаем запрос для записи движений.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.ЗаказПокупателя КАК ЗаказПокупателя,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.ЗаказПокупателя
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ЗаказПокупателя
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Товары.Сумма КАК Сумма
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
		Движение = Движения.ЗаказыПокупателей.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.ЗаказПокупателя = Выборка.ЗаказПокупателя;
		Движение.Количество = Выборка.Количество;		
	КонецЦикла;
	
	Движения.Записать();
	
	// Контроль появления отрицательных остатков в регистре
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Номенклатура.Представление КАК Номенклатура,
		|	ЗаказыПокупателейОстатки.ЗаказПокупателя.Представление КАК ЗаказПокупателя,
		|	-ЗаказыПокупателейОстатки.КоличествоОстаток КАК Превышение
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(
		|			&МоментВремени,
		|			(Номенклатура, ЗаказПокупателя) В
		|				(ВЫБРАТЬ
		|					Т.Номенклатура,
		|					Т.ЗаказПокупателя
		|				ИЗ
		|					ВТ_Товары КАК Т)) КАК ЗаказыПокупателейОстатки
		|ГДЕ
		|	ЗаказыПокупателейОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Обнаружено превышение по товару %1 в количестве %2 по заказу %3",
			Выборка.Номенклатура, Выборка.Превышение, Выборка.ЗаказПокупателя);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.ЗаказПокупателя = ТекСтрокаСписокНоменклатуры.ЗаказПокупателя;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
		Движение.Сумма = ТекСтрокаСписокНоменклатуры.Сумма;
	КонецЦикла;

		
КонецПроцедуры

```

## 5. Документ Расходная накладная

В шапку документа добавляем Заказ покупателя.
Документ делает движения по регистру Остатки Номенклатуры.

### 5.1 Модуль объекта

```1C

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		// Заполнение шапки
		ЗаказПокупателя = ДанныеЗаполнения.Ссылка;
		Для Каждого ТекСтрокаСписокНоменклатуры Из ДанныеЗаполнения.СписокНоменклатуры Цикл
			НоваяСтрока = СписокНоменклатуры.Добавить();
			НоваяСтрока.Количество = ТекСтрокаСписокНоменклатуры.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	//Проводим по старой методике
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	//устанавливаем блокировку
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("ЗаказПокупателя", ЗаказПокупателя);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	//Делаем запрос на получение данных для движений
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК КоличествоВДокументе,
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка.ЗаказПокупателя КАК ЗаказПокупателя
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка.ЗаказПокупателя
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ВТ_Товары.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ВТ_Товары.ЗаказПокупателя.Представление КАК ЗаказПокупателяПредставление,
		|	ВТ_Товары.КоличествоВДокументе КАК КоличествоВДокументе,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				(Номенклатура, ЗаказПокупателя) В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура КАК Номенклатура,
		|						ВТ_Товары.ЗаказПокупателя КАК ЗаказПокупателя
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО ВТ_Товары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Превышение = Выборка.КоличествоВДокументе - Выборка.КоличествоОстаток;
		
		Если Превышение > 0  Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Превышен остаток по товару %1 и заказу %2  в количестве %3",
				Выборка.НоменклатураПредставление, Выборка.ЗаказПокупателяПредставление, Превышение);
			Сообщение.Сообщить();
			
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;	
		
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.ЗаказПокупателя = ЗаказПокупателя;
		Движение.Количество = Выборка.КоличествоВДокументе;
		Движение.Сумма = Выборка.КоличествоВДокументе / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;

	КонецЦикла;
	

КонецПроцедуры

```


## 6. ПВХ Характеристики объектов

Переименовываем СвойстваОбъектов в ХарактеристикиОбъектов.

![[Pasted image 20241115075959.png]]


### 6.1 Создаем справочник ЗначенияХарактеристик

### 6.2 Создаем РС ЗначенияХарактеристикЗаказов

![[Pasted image 20241115080431.png]]

