
## Текст задачи

Компания занимается оптовой торговлей. Все операции отражаются документами «Приходная накладная», «Расходная накладная», «Приход денег» и «Расход денег», причем для каждого из них может быть указана своя статья затрат. Таким образом, документ «Расход денег» может отражать операции, например, по оплате поставщику или возврата от покупателя в зависимости от выбранной статьи затрат.

Структурно компания состоит из нескольких подразделений. В целях ведения управленческого учета для каждого отдела заводится бюджет (документ «Бюджет») на  произвольный ~~месяц~~ период (указывается в шапке документа) с указанием всех предполагаемых статей затрат, сумм по ним и возможного превышения этих сумм в суммовом выражении. Следует считать, что каждое подразделение может вводить несколько документов «Бюджет», данные которых должны суммироваться для формирования итогового бюджета.

В целях контроля над исполнением бюджета при проведении любого документа должен происходить анализ фактических затрат и выводится соответствующее предупреждение, в случае превышения над бюджетом. Например, если по статье «оплата поставщикам» запланировано 100 000 и 20 000, то при суммарной оплате более 100 000 и более 120 000 должны быть выданы соответствующие предупреждения.

Можно считать, что документы задним числом не вводятся, но существующие документы могут быть перепроведены.

Необходимо создать отчет по исполнению бюджета за период, кратный месяцу.

Исполнение бюджета за период с 01.01.2010 по 29.02.2010

![[Pasted image 20241105212551.png]]

«% исп.» рассчитывается как процент фактических затрат относительно плановых.

## 2.0 Структура Метаданных

### 2.1  Заводим справочники Подразделения и СтатьиЗатрат
![[Pasted image 20241105213101.png]]

### 2.2 Создаем документ Бюджет

![[Pasted image 20241105213157.png]]

![[Pasted image 20241105213217.png]]

### 2.2.1 Модуль объекта Документ Бюджет

```1C

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ ПЛАН ПО БЮДЖЕТУ
	
	Движения.Бюджет.Записывать = Истина; 
	
	// Заполнения соответствия ДоляБюджетаПоМесяцам
	// Ключ соответствия - первое число месяца, значение - доля дней в месяце от общего количества дней периода
	ДоляБюджетаПоМесяцам = Новый Соответствие;	
	НачалоТекущегоПериода = НачалоПериода;	
	ДнейЗаВесьПериод = (КонецПериода - НачалоПериода) / 86400 + 1;	
	Пока НачалоТекущегоПериода < КонецПериода Цикл	
		ОкончаниеТекущегоПериода = Мин(КонецМесяца(НачалоТекущегоПериода), КонецДня(КонецПериода));
		
		ДнейВТекущемМесяце = (ОкончаниеТекущегоПериода + 1 - НачалоТекущегоПериода) / 86400;
		ДоляБюджетаПоМесяцам.Вставить(НачалоМесяца(НачалоТекущегоПериода), ДнейВТекущемМесяце / ДнейЗаВесьПериод);
		
		НачалоТекущегоПериода = ОкончаниеТекущегоПериода + 1;		
	КонецЦикла; 
	
	Для Каждого СтрокаТЧ Из СписокСтатей Цикл
		СуммаВРегистре = 0; // для контроля распределения всей суммы, указанной в реквизите Сумма табличной части
		ПревышениеВРегистре = 0; // для контроля распределения всей суммы, указанной в реквизите Превышение табличной части
		
		// Распределяем сумму в строке ТЧ по месяцам пропорционально определенной ранее доле каждого месяца
		Для Каждого ДоляБюджетаМесяца Из ДоляБюджетаПоМесяцам Цикл		
			Движение = Движения.Бюджет.Добавить();
			Движение.Период = ДоляБюджетаМесяца.Ключ;
			Движение.Подразделение = Подразделение;
			Движение.СтатьяЗатрат = СтрокаТЧ.СтатьяЗатрат;
			Движение.План = ДоляБюджетаМесяца.Значение * СтрокаТЧ.Сумма;
			Движение.ДопустимоеПревышение = ДоляБюджетаМесяца.Значение * СтрокаТЧ.Превышение;
			
			СуммаВРегистре = СуммаВРегистре + Движение.План;
			ПревышениеВРегистре = ПревышениеВРегистре + Движение.ДопустимоеПревышение;
		КонецЦикла;
		
		// Проверка распределения всей суммы, указанной в строке табличной части в поле Сумма
		РасхождениеСумма = СтрокаТЧ.Сумма - СуммаВРегистре;
		Если РасхождениеСумма <> 0 Тогда
			Движение.План = Движение.План + РасхождениеСумма;
		КонецЕсли;
		
		// Проверка распределения всей суммы, указанной в строке табличной части в поле Превышение
		РасхождениеПревышение = СтрокаТЧ.Превышение - ПревышениеВРегистре;
		Если РасхождениеПревышение <> 0 Тогда
			Движение.ДопустимоеПревышение = Движение.ДопустимоеПревышение + РасхождениеПревышение;
		КонецЕсли;
	КонецЦикла;
		
		
КонецПроцедуры

```

## 2.3 Создаем оборотный РН Бюджет

![[Pasted image 20241105214009.png]]

## 2.4 Создаем Документы Приходная Накладная и Расходная Накладная

Движения должны делать движения по РН Бюджет.
По  РН ОстаткиТоваров делать не обязательно.

![[Pasted image 20241105221034.png]]


## 2.5 Создаем документы Приход и Расход Денег
Движения должны делать движения по РН Бюджет.

![[Pasted image 20241105221658.png]]


## 3. Создаем общий модуль Оперативный учет.

Оставляем в свойствах только галочку сервер.

### 3.1 Текст модуля

```1C

// Через подписку на событие обработки проведения вызывается сразу для 4ых документов (Приходная и Расходная накладная, Приход и Расход денег)
Процедура ОбработкаПроведенияДокументовКонтрольБюджета(Источник, Отказ, РежимПроведения) Экспорт
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ БЮДЖЕТ
	
	ДвиженияБюджет = Источник.Движения.Бюджет;
	
	ДвиженияБюджет.Записывать = Истина;
	ДвиженияБюджет.БлокироватьДляИзменения = Истина;
	
	// Отражение движения по регистру Бюджет по подразделению, Статье и сумме документа
	Движение = ДвиженияБюджет.Добавить();
	Движение.Период = Источник.Дата;
	Движение.Подразделение = Источник.Подразделение;
	Движение.СтатьяЗатрат = Источник.СтатьяЗатрат;
	Движение.Факт = Источник.СуммаПоДокументу;
	
	// Запись движений в регистр
	Источник.Движения.Записать();	
	
	// Запрос контроля превышения планируемой суммы бюджета 
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БюджетОбороты.ФактОборот - БюджетОбороты.ПланОборот КАК ДопустимоеПревышение,
	|	БюджетОбороты.ФактОборот - БюджетОбороты.ПланОборот - БюджетОбороты.ДопустимоеПревышениеОборот КАК ПревышениеСверхДопустимого
	|ИЗ
	|	РегистрНакопления.Бюджет.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Подразделение = &Подразделение
	|				И СтатьяЗатрат = &СтатьяЗатрат) КАК БюджетОбороты
	|ГДЕ
	|	БюджетОбороты.ФактОборот > БюджетОбороты.ПланОборот";
	
	ДатаДокумента = Источник.Дата;
	
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ДатаДокумента));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ДатаДокумента));
	Запрос.УстановитьПараметр("Подразделение", Источник.Подразделение);
	Запрос.УстановитьПараметр("СтатьяЗатрат", Источник.СтатьяЗатрат);
	
	// Обход результатов запроса
	РезультатЗапроса = Запрос.Выполнить();			
	Если Не РезультатЗапроса.Пустой() Тогда
		// Бюджет превышен, выводим соответствующее сообщение о превышении
		Данные = РезультатЗапроса.Выбрать();	
		Данные.Следующий();
		
		Если Данные.ПревышениеСверхДопустимого > 0 Тогда
			ТекстСообщения = СтрШаблон("Превышение запланированного бюджета сверх допустимого на %1",
				Данные.ПревышениеСверхДопустимого);
		Иначе
			ТекстСообщения = СтрШаблон("Превышение запланированного бюджета в пределах допустимого на %1",
				Данные.ДопустимоеПревышение);	
		КонецЕсли;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

```

### 4. Создаем подписку на событие ОбработкаПроведенияДокументовКонтрольБюджета

![[Pasted image 20241105221948.png]]


## 5. Отчет ИсполнениеБюджета

### 5.1 Текст отчета

```1c
ВЫБРАТЬ
	БюджетОбороты.Подразделение КАК Подразделение,
	БюджетОбороты.СтатьяЗатрат КАК Статья,
	БюджетОбороты.ПланОборот КАК План,
	БюджетОбороты.ФактОборот КАК Факт,
	БюджетОбороты.Период КАК Месяц
ИЗ
	РегистрНакопления.Бюджет.Обороты(, , Месяц, ) КАК БюджетОбороты

```

![[Pasted image 20241105224537.png]]


![[Pasted image 20241105224548.png]]

![[Pasted image 20241105224557.png]]

![[Pasted image 20241105224607.png]]

![[Pasted image 20241105224619.png]]

