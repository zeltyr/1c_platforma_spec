
## Структура метаданных

![[Pasted image 20241025220137.png]]

![[Pasted image 20241025220159.png]]


![[Pasted image 20241025220807.png]]

## Модули объектов документов

### Модуль объекта Приход денег

```1C

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ОбработкаПроведенияОУ(Отказ, РежимПроведения);
	
КонецПроцедуры


Процедура ОбработкаПроведенияОУ(Отказ, РежимПроведения)

	//Старая методика.
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	
	//блокировка данных
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	//запрос получения данных
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Проект КАК Проект,
		|	ВзаиморасчетыОстатки.Проект.Валюта КАК Валюта,
		|	ВзаиморасчетыОстатки.СуммаРубОстаток КАК СуммаРубОстаток,
		|	ВзаиморасчетыОстатки.СуммаВалОстаток КАК СуммаВалОстаток
		|ПОМЕСТИТЬ Вт_Остатки
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Проект <> ЗНАЧЕНИЕ(Справочник.Проекты.Аванс)) КАК ВзаиморасчетыОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Вт_Остатки.Проект КАК Проект,
		|	Вт_Остатки.Валюта КАК Валюта,
		|	Вт_Остатки.СуммаРубОстаток КАК СуммаРуб,
		|	Вт_Остатки.СуммаВалОстаток КАК СуммаВал,
		|	Вт_Остатки.Проект.Представление КАК ПроектПредставление,
		|	Вт_Остатки.Валюта.Представление КАК ВалютаПредставление,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс
		|ИЗ
		|	Вт_Остатки КАК Вт_Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						Вт_Остатки.Валюта КАК Валюта
		|					ИЗ
		|						Вт_Остатки КАК Вт_Остатки)) КАК КурсыВалютСрезПоследних
		|		ПО Вт_Остатки.Валюта = КурсыВалютСрезПоследних.Валюта";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Период", Дата);
	
	ОсталосьСписать = СуммаПоДокументу;
	Рубль = Справочники.Валюты.РоссийскийРубль;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл 
		
		//4. Контроля остатков нет.
		Если Выборка.Курс = 0 И Выборка.Валюта <> Рубль Тогда 
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "По проекту " + Выборка.ПроектП + " для валюты " + Выборка.ВалютаП + " не задан курс";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли; 
		Курс = ?(Выборка.Курс = 0, 1, Выборка.Курс);
		Если Отказ Тогда 
			Продолжить;
		КонецЕсли;
		
		СуммаСписанияРуб = Мин(ОсталосьСписать, Выборка.СуммаРуб);
		Если СуммаСписанияРуб = Выборка.СуммаРуб Тогда 
			СуммаСписанияВал = Выборка.СуммаВал;
		Иначе 
			СуммаСписанияВал = СуммаСписанияРуб / Курс;
		КонецЕсли;

		
		//5.1 Формирование движений. Погашение долгов
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Выборка.Проект;
		Движение.СуммаРуб = СуммаСписанияРуб;
		Движение.СуммаВал = СуммаСписанияВал;
		
		ОсталосьСписать = ОсталосьСписать - Движение.СуммаРуб;
	КонецЦикла;
		
	
	//5.2 Формирование движений. Зачисление аванса
	Если Не Отказ И ОсталосьСписать > 0 Тогда 
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Справочники.Проекты.Аванс;
		Движение.СуммаРуб = ОсталосьСписать;
		Движение.СуммаВал = 0;
	КонецЕсли;
	

	
КонецПроцедуры	
```


### Модуль объекта Расходная накладная

```1C
Процедура ОбработкаПроведенияОУ(Отказ, Режим)

	// Получаем Курс валюты на дату документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладная.Проект.Валюта КАК Валюта,
		|	РасходнаяНакладная.Проект.Валюта.Представление КАК ВалютаПредставление
		|ПОМЕСТИТЬ ВТ_ВалютаПроектв
		|ИЗ
		|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВалютаПроектв.Валюта КАК Валюта,
		|	ВТ_ВалютаПроектв.ВалютаПредставление КАК ВалютаПредставление,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс
		|ИЗ
		|	ВТ_ВалютаПроектв КАК ВТ_ВалютаПроектв
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						ВТ_ВалютаПроектв.Валюта
		|					ИЗ
		|						ВТ_ВалютаПроектв КАК ВТ_ВалютаПроектв)) КАК КурсыВалютСрезПоследних
		|		ПО ВТ_ВалютаПроектв.Валюта = КурсыВалютСрезПоследних.Валюта";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Обходим выборку и если валюта не рубль то выдаем ошибку при нулевом курсе
		Если Выборка.Курс = 0 И Выборка.Валюта <> Справочники.Валюты.РоссийскийРубль Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Для валюты " + Выборка.ВалютаПредставление + " не указан курс";;
			Сообщение.Сообщить();
		Иначе
			Курс = ?(Выборка.Курс = 0, 1, Выборка.Курс);
		КонецЕсли;
	КонецЦикла;
	
	// Проводим по старой методике
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	
	//Устанавливаем блокировкуна регистр
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Проект", Проект);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	//Получаем аванс
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	-ВзаиморасчетыОстатки.СуммаРубОстаток КАК СуммаРуб
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Проект = &ПроектАванс) КАК ВзаиморасчетыОстатки
		|ГДЕ
		|	ВзаиморасчетыОстатки.СуммаРубОстаток <> 0";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("ПроектАванс", Справочники.Проекты.Аванс);
	
	РезультатЗапроса = Запрос.Выполнить();
	ОсталосьСписать  =  СуммаПоДокументу * Курс;
	СуммаСписания = 0;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		СуммаСписания = Мин(ОсталосьСписать, Выборка.СуммаРуб);
			
		//5.1 Формирование движнний. Уменьшение аванса
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Справочники.Проекты.Аванс;
		Движение.СуммаРуб = СуммаСписания;
		Движение.СуммаВал = 0;
		
	КонецЕсли;
	
	//Формирование движений. Отражение долга
	ОсталосьСписать = ОсталосьСписать - СуммаСписания;
	Если ОсталосьСписать > 0 Тогда 
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Проект;
		Движение.СуммаРуб = ОсталосьСписать;
		Движение.СуммаВал = ОсталосьСписать / Курс;
	КонецЕсли;	
	

КонецПроцедуры

```

## Отчет СКД

### Запрос отчета

```1C
ВЫБРАТЬ
	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
	ВЫБОР
		КОГДА ВзаиморасчетыОстатки.Проект = &Аванс
			ТОГДА "Аванс"
		ИНАЧЕ ВзаиморасчетыОстатки.Проект
	КОНЕЦ КАК Проект,
	ВЫБОР
		КОГДА ВзаиморасчетыОстатки.Проект = &Аванс
			ТОГДА -ВзаиморасчетыОстатки.СуммаРубОстаток
		ИНАЧЕ ВзаиморасчетыОстатки.СуммаРубОстаток
	КОНЕЦ КАК СуммаРуб,
	ВзаиморасчетыОстатки.СуммаВалОстаток КАК СуммаВал
ИЗ
	РегистрНакопления.Взаиморасчеты.Остатки КАК ВзаиморасчетыОстатки
```

### Настройки СКД

![[Pasted image 20241025221446.png]]

![[Pasted image 20241025221455.png]]

![[Pasted image 20241025221514.png]]

![[Pasted image 20241025221531.png]]

![[Pasted image 20241025221549.png]]

