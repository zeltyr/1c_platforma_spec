## Документ изменение учетной политики
Периодичность документа месяца, в модуле объекта перед записью дату приводим к началу месяца.
Основная идея такова, при переходе с FIFO на ПоСредней нужно списать все остатки по партиям и оприходовать их на одну партию в которой Партия равна ссылке на тек документ Изменение УчетнойПолитики.


```1С

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	// Так как периодичность регистра - месяц, то чтобы избежать ошибки при вводе пользовательских данных, 
	// принудительно устанавливаем дату в начало месяца. Тогда не будет расхождений в дате документа,
	// даты установки учетной политики и даты получения и записи данных в регистр остатков.
	Дата = НачалоМесяца(Дата);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ УЧЕТНАЯ ПОЛИТИКА
	Движения.УчетнаяПолитика.Записывать = Истина;
	
	// Отражение в регистре Учетная политика метода списания, указанного в документе
	Движение = Движения.УчетнаяПолитика.Добавить();
	Движение.Период = Дата;
	Движение.МетодСписания = МетодСписания;
	
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ ОСТАТКИ НОМЕНКЛАТУРЫ            	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	// Если устанавливаемый метод списаний - по средней, то получаем все остатки из регистра по партиям, 
	// списываем их и помещаем их на одну партию - сам документ установки учетной политики
	Если МетодСписания = Перечисления.МетодыСписания.ПоСредней Тогда
		
		Движения.ОстаткиНоменклатуры.Записать();
		
		// Установка блокировки данных в регистре Остатки номенклатуры по всем записям
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		// Запрос для формирования движений. Итоги для того, чтобы минимизировать количество записей помещаемых в регистр
		// по оприходованию остатков: данные по одной номенклатуре разных партий будут записаны одной строкой
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментИтогов, ) КАК ОстаткиНоменклатурыОстатки
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Сумма)
		|ПО
		|	Номенклатура";
		
		Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
		
		// Обход результатов запроса
		ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			// Оприходование остатков на одну партию - текущий документ
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.Период = Дата;
			ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
			Движение.Партия = Ссылка;
			
			ВыборкаДетальные = ВыборкаНоменклатура.Выбрать();
			Пока ВыборкаДетальные.Следующий() Цикл
				// Списание имеющихся движений по партиям
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				Движение.Период = Дата;
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальные);
			КонецЦикла;
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры


```

### Структура документа Изменение учетной политики

![[Pasted image 20241016224539.png]]

### Структура регистра накопления

![[Pasted image 20241016224822.png]]
### Документ Приходная накладная

![[Pasted image 20241016225004.png]]

Модуль объекта документа
```1C

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ ОСТАТКИ НОМЕНКЛАТУРЫ

	// Получение текущего метода списания и регистратора установки метода списания
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаСрезПоследних.Регистратор,
	|	УчетнаяПолитикаСрезПоследних.МетодСписания
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика.СрезПоследних(&ДатаСреза, ) КАК УчетнаяПолитикаСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаСреза", Дата);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнена учетная политика.";
		Сообщение.Сообщить();
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Если текущий метод списания - по средней, то в качестве партии берем документ установки партии, иначе - текущий документ
	Данные = Результат.Выбрать();
	Данные.Следующий();
	Партия = ?(Данные.МетодСписания = Перечисления.МетодыСписания.ПоСредней, Данные.Регистратор, Ссылка);
	
	// Формирование набора записей регистра на основании сгруппированных строк табличной части
	Движения.ОстаткиНоменклатуры.Записывать = Истина;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	 |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	 |	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	 |ИЗ
	 |	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
	 |ГДЕ
	 |	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Партия = Партия;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Выборка.Сумма;
	КонецЦикла;
	
КонецПроцедуры


```
### Документ расходная накладная 

![[Pasted image 20241016225151.png]]

Модуль объекта документа

```1C

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаДокумента = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	ОбработкаПроведенияОУ(Отказ);
	ОбработкаПроведенияБУ(Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведенияОУ(Отказ)
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ ОСТАТКИ НОМЕНКЛАТУРЫ (СТАРАЯ МЕТОДИКА ПРОВЕДЕНИЯ)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	// Установка блокировки данных в регистре Остатки номенклатуры по списку номенклатур табличной части
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	// Запрос получения данных для формирования движений
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МИНИМУМ(РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки) КАК НомерСтроки,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ТабЧасть
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры <> &ВидНоменклатурыУслуга
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТабЧасть.НомерСтроки КАК НомерСтроки,
	|	ВТ_ТабЧасть.Номенклатура КАК Номенклатура,
	|	ВТ_ТабЧасть.Количество КАК КоличествоВДокументе,
	|	ВТ_ТабЧасть.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	|ИЗ
	|	ВТ_ТабЧасть КАК ВТ_ТабЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						Т.Номенклатура
	|					ИЗ
	|						ВТ_ТабЧасть КАК Т)) КАК ОстаткиНоменклатурыОстатки
	|		ПО ВТ_ТабЧасть.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
	|ИТОГИ
	|	МАКСИМУМ(НомерСтроки),
	|	МАКСИМУМ(КоличествоВДокументе),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("ВидНоменклатурыУслуга", Перечисления.ВидыНоменклатуры.Услуга);
	
	// Обход результатов запроса
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		// Контроль наличия номенклатуры
		Если ВыборкаНоменклатура.КоличествоВДокументе > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре %1. Имеющийся остаток: %2",
				ВыборкаНоменклатура.НоменклатураПредставление, ВыборкаНоменклатура.КоличествоОстаток);						
			// Привязка выводимого сообщения к полю формы (требование задачи по управляемым формам)	
			Сообщение.Поле = "Объект.СписокНоменклатуры["+(ВыборкаНоменклатура.НомерСтроки-1)+"].Количество";
			Сообщение.Сообщить();
			
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда 
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.КоличествоВДокументе;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ОсталосьСписать <> 0 Цикл
			
			// Движение по регистру Остатки номенклатуры
			Списываем = Мин (Выборка.КоличествоОстаток, ОсталосьСписать);
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Партия = Выборка.Партия;
			
			Движение.Количество = Списываем;
			Движение.Сумма = Списываем / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			
			ОсталосьСписать = ОсталосьСписать - Списываем;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
	
Процедура ОбработкаПроведенияБУ(Отказ)
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ УПРАВЛЕНЧЕСКИЙ
	
	Движения.Управленческий.Записывать = Истина;
	
	СтруктураОтбора = Новый Структура ("Валюта", Договор.Валюта);
	КурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, СтруктураОтбора).Курс;
	Если Не ЗначениеЗаполнено(КурсВалюты) Тогда
		КурсВалюты = 1;
	КонецЕсли;
	
	Проводка = Движения.Управленческий.Добавить();
	Проводка.Период = Дата;
	Проводка.СчетДт = ПланыСчетов.Управленческий.Покупатели;
	Проводка.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
	Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
	Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;
	Проводка.СуммаВалютнаяДт = СуммаДокумента;
	Проводка.Сумма = СуммаДокумента * КурсВалюты;
	
КонецПроцедуры


```

### Отчет ОУ
#### Запрос отчета
``` 1C
ВЫБРАТЬ
	ОстаткиНоменклатурыОстаткиИОбороты.Номенклатура КАК Номенклатура,
	ОстаткиНоменклатурыОстаткиИОбороты.Партия КАК Партия,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоНачальныйОстаток КАК КоличествоНачальныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоКонечныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоПриход КАК КоличествоПриход,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоРасход КАК КоличествоРасход,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаПриход КАК СуммаПриход,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаРасход КАК СуммаРасход
ИЗ
	РегистрНакопления.ОстаткиНоменклатуры.ОстаткиИОбороты КАК ОстаткиНоменклатурыОстаткиИОбороты
```

#### Вкладка Ресурсы

![[Pasted image 20241016225325.png]]

#### Вкладка Параметры

![[Pasted image 20241016225352.png]]

#### Вкладка Макеты

![[Pasted image 20241016225410.png]]

#### Вкладка  Настройки
![[Pasted image 20241016225437.png]]

![[Pasted image 20241016225457.png]]