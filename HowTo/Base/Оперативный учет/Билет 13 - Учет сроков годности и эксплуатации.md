
## 1. Регистр накопления "Остатки номенклатуры"

Добавляем измерение ==СрокГодности== . 

Структура оегистра

**Измерения:**
- Номенклатура
- СрокГодности
**Ресурсы:**
- Количество
- Стоимость

## 2. Документ "Приходная накладная"

В ТЧ товары добавляем колонку "Срок годности".

### 2.1 Модуль объекта

```1C
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ОстаткиНоменклатуры.Записывать = ИСТИНА;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.СрокГодности КАК СрокГодности,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
		|	&Период КАК Период
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.СрокГодности";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение,ВыборкаДетальныеЗаписи);
	КонецЦикла;

КонецПроцедуры
```


## 3. Справочник "Номенклатура"

Добавляем реквизит ==Срок Эксплуатации==  с типом число

## 4. Документ "Ввод в эксплуатацию"

Добавляем табличную часть СписокНоменклатуры с 2 колонками.
Номенклатура и Количество.

![[Pasted image 20241116130430.png]]

### 4.1 Модуль объекта

```1C

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Старая методика для Регистра Остатки Номенклатуры
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	Движения.ЭксплуатируемоеОборудование.Записывать = Истина;
	
	//Устанавливаем блокировку

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(Дата));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();	
	
	//Пишем запрос для получения данных
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ВводВЭксплуатациюСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.ВводВЭксплуатацию.СписокНоменклатуры КАК ВводВЭксплуатациюСписокНоменклатуры
		|ГДЕ
		|	ВводВЭксплуатациюСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ДОБАВИТЬКДАТЕ(&Дата, ДЕНЬ, ВТ_Товары.Номенклатура.СрокЭксплуатации) КАК СрокЭксплуатации,
		|	ВТ_Товары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ВТ_Товары.Количество КАК КоличествоВДокументе,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|						(ВЫБРАТЬ
		|							ВТ_Товары.Номенклатура КАК Номенклатура
		|						ИЗ
		|							ВТ_Товары КАК ВТ_Товары)
		|					И СрокГодности > &Дата) КАК ОстаткиНоменклатурыОстатки
		|		ПО ВТ_Товары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СрокГодности
		|ИТОГИ
		|	МАКСИМУМ(КоличествоВДокументе),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		//Контролируем превышение
		Превышение = ВыборкаНоменклатура.КоличествоВДокументе - ВыборкаНоменклатура.КоличествоОстаток;
		
		Если Превышение > 0 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Превышение остатка по номенлкатура %1  в количестве %2",
				ВыборкаНоменклатура.НоменклатураПредставление, Превышение);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;	
		КонецЕсли;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		ОсталосьСписать = ВыборкаНоменклатура.КоличествоВДокументе;
		
		Пока Выборка.Следующий() Цикл
			
			Списываем = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
			
			//Движения по регистру остатки
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.СрокГодности = Выборка.СрокГодности;
			Движение.Количество = Списываем;
			Движение.Сумма = Списываем / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			
			//ДВижения по региструу Экспл оборудование
			ДвижениеЭксплуатируемоеОборудование = Движения.ЭксплуатируемоеОборудование.ДобавитьПриход();
			ДвижениеЭксплуатируемоеОборудование.Номенклатура = Выборка.Номенклатура;
			ДвижениеЭксплуатируемоеОборудование.СрокГодности = Выборка.СрокГодности;
			ДвижениеЭксплуатируемоеОборудование.Период = Дата;
			ДвижениеЭксплуатируемоеОборудование.СрокЭксплуатации = Выборка.СрокЭксплуатации;
			ДвижениеЭксплуатируемоеОборудование.Количество = Списываем;
			ДвижениеЭксплуатируемоеОборудование.Сумма =  Списываем / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			
			ОсталосьСписать = ОсталосьСписать - Списываем;
		КонецЦикла;
	КонецЦикла;
	

КонецПроцедуры

```

## 5. Регистра накопления "Эксплуатируемое Оборудование"

Структура регистра:

**Измерения:**
- Номенклатура
- СрокГодности
- СрокЭксплуатации

**Ресурсы:**
- Количество
- Сумма

![[Pasted image 20241116104442.png]]


## 6 Документ ВыбытиеОборудования

Документ не имеет ни реквищитов ни тч.

### 6.1 Модуль объекта

```1с

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Движения по регистру остатки
	Движения.ОстаткиНоменклатуры.Записывать = истина;
	ДВижения.ОстаткиНоменклатуры.Записать();
	
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,Дата));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СуммаОстаток КАК Сумма,
		|	&Дата КАК Период
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, СрокГодности < &Дата) КАК ОстаткиНоменклатурыОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	//Движения по регистру Эксплуатируемое оборудование
	
	Движения.ЭксплуатируемоеОборудование.Записывать = Истина;
	Движения.ЭксплуатируемоеОборудование.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЭксплуатируемоеОборудование");
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,Дата));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЭксплуатируемоеОборудование");
	ЭлементБлокировки.УстановитьЗначение("СрокЭксплуатации", Новый Диапазон(,Дата));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Блокировка.Заблокировать();
	
	//Получение данных
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭксплуатируемоеОборудованиеОстатки.Номенклатура КАК Номенклатура,
		|	ЭксплуатируемоеОборудованиеОстатки.СрокГодности КАК СрокГодности,
		|	ЭксплуатируемоеОборудованиеОстатки.СрокЭксплуатации КАК СрокЭксплуатации,
		|	ЭксплуатируемоеОборудованиеОстатки.КоличествоОстаток КАК Количество,
		|	ЭксплуатируемоеОборудованиеОстатки.СуммаОстаток КАК Сумма,
		|	&Дата КАК Период
		|ИЗ
		|	РегистрНакопления.ЭксплуатируемоеОборудование.Остатки(
		|			&МоментВремени,
		|			СрокГодности < &Дата
		|				ИЛИ СрокЭксплуатации < &Дата) КАК ЭксплуатируемоеОборудованиеОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ЭксплуатируемоеОборудование.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		
	КонецЦикла;

КонецПроцедуры

```

## 7. Отчет  "Оборудование в эксплуатации"

Запрос отчета

```1C
ВЫБРАТЬ
	ЭксплуатируемоеОборудованиеОстатки.Номенклатура КАК Оборудование,
	РАЗНОСТЬДАТ(&Период, ЭксплуатируемоеОборудованиеОстатки.СрокГодности, ДЕНЬ) КАК ОставшийсяСрокГодности,
	РАЗНОСТЬДАТ(&Период, ЭксплуатируемоеОборудованиеОстатки.СрокЭксплуатации, ДЕНЬ) КАК ОставшийсяСрокЭксплуатации,
	ЭксплуатируемоеОборудованиеОстатки.СуммаОстаток КАК Сумма,
	ЭксплуатируемоеОборудованиеОстатки.КоличествоОстаток КАК Количество
ИЗ
	РегистрНакопления.ЭксплуатируемоеОборудование.Остатки КАК ЭксплуатируемоеОборудованиеОстатки
```

![[Pasted image 20241116222134.png]]

![[Pasted image 20241116222141.png]]

![[Pasted image 20241116222152.png]]

![[Pasted image 20241116222204.png]]

